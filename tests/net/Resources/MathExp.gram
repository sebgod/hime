grammar MathExp
{
	options
	{
		Axiom = "exp";
		Separator = "SEPARATOR";
	}
	terminals {
		INTEGER	-> [1-9] [0-9]* | '0' ;
		REAL		-> INTEGER? '.' INTEGER  (('e' | 'E') ('+' | '-')? INTEGER)?
					|  INTEGER ('e' | 'E') ('+' | '-')? INTEGER ;
		NUMBER	-> INTEGER | REAL ;

		PLUS	-> '+' ;
		MINUS	-> '-' ;
		MULT	-> '*' ;
		DIV		-> '/' ;

		WHITE_SPACE	-> U+0020 | U+0009 | U+000B | U+000C ;
		SEPARATOR	-> WHITE_SPACE+;
	}
	rules {
		exp_atom-> NUMBER^ {OnNumber}
			| '('! exp^ ')'! ;

		exp_op0	-> exp_atom^
			|  exp_op0 MULT^ exp_atom {OnMult}
			|  exp_op0 DIV^ exp_atom {OnDiv};
		
		exp_op1	-> exp_op0^
			|  exp_op1 PLUS^ exp_op0 {OnPlus}
			|  exp_op1 MINUS^ exp_op0 {OnMinus};

		exp	-> exp_op1^ ;
	}
}
